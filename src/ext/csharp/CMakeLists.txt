

foreach(SRC ${SWIG_SRCS})
    set_source_files_properties(${SRC} PROPERTIES CPLUSPLUS ON)
endforeach()


set(SWIG_MODULE_c_csharp_interop_EXTRA_DEPS ${SWIG_DEPS})
foreach(SRC ${SWIG_SRCS})
    set_source_files_properties(${SRC} PROPERTIES SWIG_FLAGS "-module;c_csharp_interop${SWIG_WORDSIZE_FLAG}")
endforeach()


set(CMAKE_SWIG_OUTDIR ${CMAKE_CURRENT_SOURCE_DIR}/src)
swig_add_module(c_csharp_interop csharp ${SWIG_SRCS})
set(EXTRA_LINKER_FLAGS)
if(MINGW)
    set(EXTRA_LINKER_FLAGS "-Wl,-add-stdcall-alias")
    get_target_property(existing_link_flags ${SWIG_MODULE_c_csharp_interop_REAL_NAME} LINK_FLAGS)
    if(existing_link_flags)
        set(EXTRA_LINKER_FLAGS "${existing_link_flags} ${EXTRA_LINKER_FLAGS}")
    endif()
    set_target_properties(${SWIG_MODULE_c_csharp_interop_REAL_NAME} PROPERTIES LINK_FLAGS "${EXTRA_LINKER_FLAGS}")
endif()
swig_link_libraries(${SWIG_MODULE_c_csharp_interop_REAL_NAME} ${INTEROP_DL_LIB})

if(WIN32)
    set_target_properties(${SWIG_MODULE_c_csharp_interop_REAL_NAME} PROPERTIES PREFIX "")
endif()
add_dependencies(${SWIG_MODULE_c_csharp_interop_REAL_NAME} ${INTEROP_LIB})

if(NOT ENABLE_STATIC)
    add_custom_command(TARGET ${SWIG_MODULE_c_csharp_interop_REAL_NAME} POST_BUILD
            COMMAND ${CMAKE_COMMAND} -E copy_if_different $<TARGET_FILE_DIR:${INTEROP_LIB}> ${CMAKE_CURRENT_BINARY_DIR})
endif()


find_package(CSharp)

if(NOT CSHARP_FOUND)
    message(WARNING "C# compiler not found, unable to generate C# binding library")
    return()
endif()

include(${CSHARP_USE_FILE})

set(SWIG_GEN_CSHARP_SOURCE_FILES ${CMAKE_SWIG_OUTDIR}/*.cs CACHE INTERNAL "C# source files generated by SWIG" FORCE)

csharp_add_library(csharp_interop ${CMAKE_SWIG_OUTDIR}/*.cs)
add_dependencies(csharp_interop ${SWIG_MODULE_c_csharp_interop_REAL_NAME})
add_custom_command(
        TARGET csharp_interop
        POST_BUILD
        COMMAND ${CMAKE_COMMAND} -E copy_if_different $<TARGET_FILE:${SWIG_MODULE_c_csharp_interop_REAL_NAME}> $<TARGET_FILE_DIR:${INTEROP_LIB}>
        COMMAND ${CMAKE_COMMAND} -E copy_if_different ${CSHARP_csharp_interop_BINARY} $<TARGET_FILE_DIR:${INTEROP_LIB}>
        COMMENT "copy ${CSHARP_csharp_interop_BINARY} $<TARGET_FILE_DIR:${INTEROP_LIB}>"
)

if(ENABLE_EXAMPLES)
    install(FILES ${CSHARP_csharp_interop_BINARY} DESTINATION share/illumina/interop/examples)
    install(TARGETS ${SWIG_MODULE_c_csharp_interop_REAL_NAME} LIBRARY DESTINATION share/illumina/interop/examples)
endif()

install(FILES ${CSHARP_csharp_interop_BINARY}
        DESTINATION lib64
)
install(TARGETS ${SWIG_MODULE_c_csharp_interop_REAL_NAME} LIBRARY DESTINATION lib64)